'Copyright (c) 2007-2013 Bo Morgan.
 All rights reserved.
 
 Authors:
 
   Bo Morgan (2007-2013): Initial implementation.
 
 This program is distributed under the terms of the GNU General
 Public License.  See the COPYING file that is distributed with this
 file.
 
 Permission to use, copy, modify and distribute this software and its
 documentation is hereby granted, provided that both the copyright
 notice and this permission notice appear in all copies of the
 software, derivative works or modified versions, and any portions
 thereof, and that both notices appear in supporting documentation.
 
 BO MORGAN ALLOWS FREE USE OF THIS SOFTWARE IN ITS "AS IS" CONDITION.
 BO MORGAN DISCLAIMS ANY LIABILITY OF ANY KIND FOR ANY DAMAGES
 WHATSOEVER RESULTING FROM THE USE OF THIS SOFTWARE.
 
 Bo Morgan requests users of this software to return to bo@mit.edu any
 improvements or extensions that they make and grant Bo Morgan the
 rights to redistribute these changes.'

'addition'

[defmetro + [x :rest rest]
  [if [null rest]
      [demetropolize x]
    [let [[rest_result [apply &+ rest]]]
      [if [and [or [is-type `integer      x]
		   [is-type `double       x]
		   [is-type `float        x]
		   [is-type `largeinteger x]]
	       [or [is-type `integer      rest_result]
		   [is-type `double       rest_result]
		   [is-type `float        rest_result]
		   [is-type `largeinteger rest_result]]]
	  [bytecode:add x rest_result]
	`[bytecode:add ,[demetropolize x] ,[demetropolize rest_result]]]]]]


'subtraction'

[defmetro negative [x]
  [if [or [is-type `integer      x]
	  [is-type `double       x]
	  [is-type `float        x]
	  [is-type `largeinteger x]]
      [bytecode:negative x]
    `[bytecode:negative ,[demetropolize x]]]]

[defmetro - [x :rest rest]
  [if [or [is-type `integer      x]
	  [is-type `double       x]
	  [is-type `float        x]
	  [is-type `largeinteger x]]
      [if [null rest]
	  [bytecode:negative x]
	[let [[rest_result [apply &+ rest]]]
	  [if [or [is-type `integer      rest_result]
		  [is-type `double       rest_result]
		  [is-type `float        rest_result]
		  [is-type `largeinteger rest_result]]
	      [bytecode:subtract x rest_result]
	    `[bytecode:subtract ,[demetropolize x] ,[demetropolize rest_result]]]]]
    [if [null rest]
	`[bytecode:negative ,[demetropolize x]]
      [let [[rest_result [apply &+ rest]]]
	`[bytecode:subtract ,[demetropolize x] ,[demetropolize rest_result]]]]]]


'multiplication'

[defmetro * [x :rest rest]
  [if [null rest]
      [demetropolize x]
    [let [[rest_result [apply &* rest]]]
      [if [and [or [is-type `integer      x]
		   [is-type `double       x]
		   [is-type `float        x]
		   [is-type `largeinteger x]]
	       [or [is-type `integer      rest_result]
		   [is-type `double       rest_result]
		   [is-type `float        rest_result]
		   [is-type `largeinteger rest_result]]]
	  [bytecode:multiply x rest_result]
	`[bytecode:multiply ,[demetropolize x] ,[demetropolize rest_result]]]]]]


'division'

[defmetro inverse [x]
  [if [or [is-type `integer      x]
	  [is-type `double       x]
	  [is-type `float        x]
	  [is-type `largeinteger x]]
      [bytecode:inverse x]
    `[bytecode:inverse ,[demetropolize x]]]]

[defmetro / [x :rest rest]
  [if [or [is-type `integer      x]
	  [is-type `double       x]
	  [is-type `float        x]
	  [is-type `largeinteger x]]
      [if [null rest]
	  [bytecode:inverse x]
	[let [[rest_result [apply &* rest]]]
	  [if [or [is-type `integer      rest_result]
		  [is-type `double       rest_result]
		  [is-type `float        rest_result]
		  [is-type `largeinteger rest_result]]
	      [bytecode:divide x rest_result]
	    `[bytecode:divide ,[demetropolize x] ,[demetropolize rest_result]]]]]
    [if [null rest]
	`[bytecode:inverse ,[demetropolize x]]
      [let [[rest_result [apply &* rest]]]
	`[bytecode:divide ,[demetropolize x] ,[demetropolize rest_result]]]]]]


'modulo'

[defmetro % [x y]
  `[bytecode:modulo ,x ,y]]


'equals'

[defmetro == [x y]
  `[bytecode:numerically_equals ,x ,y]]

[defmetro != [x y]
  `[bytecode:not [bytecode:numerically_equals ,x ,y]]]

'bitshift-left'

[defunk << [x y]
  [if [and [is-type `integer x] [is-type `integer y]] [integer-bitshift_left x y]
    [error `runtime-error `type-check-failure]]]

'bitshift-right'

[defunk >> [x y]
  [if [and [is-type `integer x] [is-type `integer y]] [integer-bitshift_right x y]
    [error `runtime-error `type-check-failure]]]

[defunk bit_and [x y]
  [if [and [is-type `integer x] [is-type `integer y]] [integer-bit_and x y]
    [error `runtime-error `type-check-failure]]]

[defunk bit_or [x y]
  [if [and [is-type `integer x] [is-type `integer y]] [integer-bit_or x y]
    [error `runtime-error `type-check-failure]]]

[defunk bit_xor [x y]
  [if [and [is-type `integer x] [is-type `integer y]] [integer-bit_xor x y]
    [error `runtime-error `type-check-failure]]]

[defunk bit_not [x]
  [if [is-type `integer x] [integer-bit_not x]
    [error `runtime-error `type-check-failure]]]


[defmetro < [x y]
  `[bytecode:less_than ,x ,y]]

[defmetro > [x y]
  `[bytecode:greater_than ,x ,y]]

[defunk <= [x y]
  [bytecode:or [bytecode:less_than          x y]
	       [bytecode:numerically_equals x y]]]

[defunk >= [x y]
  [bytecode:or [bytecode:greater_than       x y]
               [bytecode:numerically_equals x y]]]

[defunk sqrt [x]
  [get x square_root]]

[defunk double [x]
  [get x as-double]]


[defmetro += [variable value]
  `[= ,variable [bytecode:add ,variable ,value]]]

[defmetro -= [variable value]
  `[= ,variable [bytecode:subtract ,variable ,value]]]

[defmetro *= [variable value]
  `[= ,variable [bytecode:multiply ,variable ,value]]]

[defmetro /= [variable value]
  `[= ,variable [bytecode:divide ,variable ,value]]]

[defmetro >>= [variable value]
  `[= ,variable [>> ,variable ,value]]]

[defmetro <<= [variable value]
  `[= ,variable [<< ,variable ,value]]]

[defmetro ++ [variable]
  `[= ,variable [bytecode:increment ,variable]]]

[defmetro -- [variable]
  `[= ,variable [bytecode:decrement ,variable]]]

