'Copyright (c) 2007-2010 Bo Morgan.
 All rights reserved.
 
 Author: Bo Morgan
 
 Permission to use, copy, modify and distribute this software and its
 documentation is hereby granted, provided that both the copyright
 notice and this permission notice appear in all copies of the
 software, derivative works or modified versions, and any portions
 thereof, and that both notices appear in supporting documentation.
 
 BO MORGAN ALLOWS FREE USE OF THIS SOFTWARE IN ITS "AS IS" CONDITION.
 BO MORGAN DISCLAIMS ANY LIABILITY OF ANY KIND FOR ANY DAMAGES
 WHATSOEVER RESULTING FROM THE USE OF THIS SOFTWARE.
 
 Bo Morgan requests users of this software to return to bo@mit.edu any
 improvements or extensions that they make and grant Bo Morgan the
 rights to redistribute these changes.'

[deframe          resource [frame] [name fiber active_mutex active active_funk active_args result finished done]]
[deftypeconstruct resource [name active_funk]
  [set this name         name]
  [set this active_funk  active_funk]
  [set this active_mutex [new mutex]]
  [set this fiber        [fiber [funk []
				      [while [not [get this done]]
					[if [get this active]
					    [prog [let [[active_funk [get this active_funk]]]
						    [if active_funk
							[set this result [apply active_funk [get this active_args]]]
						      [format stdout '\nresource warning: active_funk is nil (name = ' [get this name] ').\n']]]
						  [set this done t]]]
					[sleep 1]]]
				nil]]
  this]

[defmetro resource [name args :rest body]
  `[new resource [quote ,name] [funk ,args @body]]]

[deftypefunk resource execute activate [:rest args]
  [let [[successful_activation nil]]
    [prog [have [get this active_mutex] lock]
	  [if [not [get this active]]
	      [prog [set this active           t]
		    [set this active_args      args]
		    [=   successful_activation t]]]
	  [have [get this active_mutex] unlock]]
    [if successful_activation
	nil
      [error object_type `resource object_typefunk `activate error_type `resource-is-already-activated]]]]

[deftypefunk resource execute finish []
  [let [[finished nil]
	[result   nil]]
    [while [not finished]
      [if [get this done]
	  [prog [have [get this active_mutex] lock]
		[=   result           [get this result]]
		[set this active      nil]
		[set this active_args nil]
		[set this result      nil]
		[set this done        nil]
		[have [get this active_mutex] unlock]
		[= finished t]]
	[sleep 1]]]
    result]]

[deftypefunk resource execute call [:rest args]
  [have-apply this activate args]
  [have this finish]]


      
    


