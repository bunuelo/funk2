'Copyright (c) 2007-2012 Bo Morgan.
 All rights reserved.
 
 Author: Bo Morgan
 
 Permission to use, copy, modify and distribute this software and its
 documentation is hereby granted, provided that both the copyright
 notice and this permission notice appear in all copies of the
 software, derivative works or modified versions, and any portions
 thereof, and that both notices appear in supporting documentation.
 
 BO MORGAN ALLOWS FREE USE OF THIS SOFTWARE IN ITS "AS IS" CONDITION.
 BO MORGAN DISCLAIMS ANY LIABILITY OF ANY KIND FOR ANY DAMAGES
 WHATSOEVER RESULTING FROM THE USE OF THIS SOFTWARE.
 
 Bo Morgan requests users of this software to return to bo@mit.edu any
 improvements or extensions that they make and grant Bo Morgan the
 rights to redistribute these changes.'


[globalize blocks_world-current_active_frame [frame]]

'blocks_world'

[deframe blocks_world [frame] [name
			       image_width
			       image_height
			       
			       [step_mutex [new mutex]]
			       blocks_world_physics]
  [new [initial-name initial-image_width initial-image_height]
       [= name         initial-name]
       [= image_width  initial-image_width]
       [= image_height initial-image_height]
       
       [have this initialize]
       
       [have blocks_world-current_active_frame add name this]
       ]]

[deftypefunk blocks_world execute initialize []
  [= blocks_world_physics [new blocks_world_physics 8 5.5]]
  [have this setup_default_scenario]]

[deftypefunk blocks_world execute destroy []
  `[have blocks_world-current_active_frame remove name]
  ]

[deftypefunk blocks_world execute calculate_perceptions []
  [have blocks_world_physics calculate_perceptions]]

[deftypefunk blocks_world execute step [step_size]
  [have step_mutex lock]
  [have blocks_world_physics handle_movement step_size]
  [have blocks_world_physics step            step_size]
  [have this calculate_perceptions]
  [have step_mutex unlock]]

[deftypefunk blocks_world execute raw_step [step_size]
  [have step_mutex lock]
  [have blocks_world_physics handle_movement step_size]
  [have blocks_world_physics step            step_size]
  [have step_mutex unlock]]

[deftypefunk blocks_world get all_perception_symbols []
  `[Block-1
    Block-2
    Block-3
    Block-4
    Gripper-1
    Gripper-2
    Table-1
    left-of          right-of on below-left below below-right is-holding
    shape            cube pyramid rectangle
    is-a             block gripper table
    is               me
    movement_command stop left right grab recoil drop
    color            brown blue green red white black
    ]]

[deftypefunk blocks_world execute export_pdf_file [filename]
  [let* [[pdf_surface       [new cairo_pdf_surface filename [get image_width as-double] [get image_height as-double]]]
	 [pdf_cairo_context [new cairo_context pdf_surface]]]
    [have blocks_world_physics render_to_cairo pdf_cairo_context image_width image_height]
    [have pdf_surface       destroy]
    [have pdf_cairo_context destroy]]]

[deftypefunk blocks_world get as-image []
  [let* [[cairo_image_surface [new cairo_image_surface `ARGB32 image_width image_height]]
	 [cairo_context       [new cairo_context cairo_image_surface]]]
    [have blocks_world_physics render_to_cairo cairo_context image_width image_height]
    [let [[image [get cairo_image_surface as-image]]]
      [have cairo_image_surface destroy]
      [have cairo_context       destroy]
      image]]]

[deftypefunk blocks_world get gripper [name]
  [get blocks_world_physics gripper name]]

[deftypefunk blocks_world get grippers []
  [get blocks_world_physics grippers]]

[deftypefunk blocks_world execute setup_large_scenario []
  [set blocks_world_physics size 9 6.5]
  [let [[gripper-1 [new blocks_world_gripper `Gripper-1 1 1    `black `gripper   2 0.5]]
	[block-1   [new blocks_world_block   `Block-1   1 1    `brown `cube      1 5]]
	[block-2   [new blocks_world_block   `Block-2   1 1    `blue  `pyramid   4 4]]
	[block-3   [new blocks_world_block   `Block-3   1 1    `green `cube      4 5]]
	[block-4   [new blocks_world_block   `Block-4   1 1    `red   `pyramid   5 5]]
	[table-1   [new blocks_world_table   `Table-1   9 0.25 `white `rectangle 0 6]]]
    [have blocks_world_physics add_gripper gripper-1]
    [have blocks_world_physics add_block   block-1]
    [have blocks_world_physics add_block   block-2]
    [have blocks_world_physics add_block   block-3]
    [have blocks_world_physics add_block   block-4]
    [have blocks_world_physics add_block   table-1]
    ]]

[deftypefunk blocks_world execute setup_small_scenario []
  [set blocks_world_physics size 6 4.5]
  [let [[gripper-1 [new blocks_world_gripper `Gripper-1 1 1    `black `gripper 2.5 0.5]]
	[block-1   [new blocks_world_block   `Block-1   1 1    `blue  `cube    1   3]]
	[block-2   [new blocks_world_block   `Block-2   1 1    `green `pyramid 4   3]]
	[table-1   [new blocks_world_table   `Table-1   6 0.25 `white `cube    0   4]]]
    [have blocks_world_physics add_gripper gripper-1]
    [have blocks_world_physics add_block   block-1]
    [have blocks_world_physics add_block   block-2]
    [have blocks_world_physics add_block   table-1]
    ]]

[deftypefunk blocks_world execute screenshot-1 []
  [set blocks_world_physics size 6 4.5]
  [let [[gripper-1 [new blocks_world_gripper `Gripper-1 1 1    `black `gripper 2 0.5]]
	[block-1   [new blocks_world_block   `Block-1   1 1    `brown `cube    1 3]]
	[block-2   [new blocks_world_block   `Block-2   1 1    `blue  `cube    4 3]]
	[block-3   [new blocks_world_block   `Block-3   1 1    `green `pyramid 4 1.5]]
	[table-1   [new blocks_world_table   `Table-1   6 0.25 `white `cube    0 4]]]
    [have blocks_world_physics add_gripper gripper-1]
    [have blocks_world_physics add_block   block-1]
    [have blocks_world_physics add_block   block-2]
    [have blocks_world_physics add_block   block-3]
    [have blocks_world_physics add_block   table-1]
    ]]

[deftypefunk blocks_world execute setup_default_scenario []
  [have this setup_large_scenario]]



[defmetro blocks_world []
  `[prog [new blocks_world_window [new blocks_world [gensym 'blocks_world'] 640 480]]
	 nil]]


