'Copyright (c) 2007-2010 Bo Morgan.
 All rights reserved.
 
 Author: Bo Morgan
 
 Permission to use, copy, modify and distribute this software and its
 documentation is hereby granted, provided that both the copyright
 notice and this permission notice appear in all copies of the
 software, derivative works or modified versions, and any portions
 thereof, and that both notices appear in supporting documentation.
 
 BO MORGAN ALLOWS FREE USE OF THIS SOFTWARE IN ITS "AS IS" CONDITION.
 BO MORGAN DISCLAIMS ANY LIABILITY OF ANY KIND FOR ANY DAMAGES
 WHATSOEVER RESULTING FROM THE USE OF THIS SOFTWARE.
 
 Bo Morgan requests users of this software to return to bo@mit.edu any
 improvements or extensions that they make and grant Bo Morgan the
 rights to redistribute these changes.'

[defunk stream-try_read_source_expression [this]
  [let [[first_line   [get this line_number]]
	[first_column [get this column_number]]]
    [let [[read_result [stream-try_read this]]]
      [if [and [is-type `exception read_result] [eq [get read_result tag] `reader:end_of_file-exception]]
	  nil
	[let [[body           read_result]
	      [last_line      [get this line_number]]
	      [last_column    [get this column_number]]
	      [subexpressions nil]]
	  [stream-skip_whitespace this]
	  [new source_expression body first_line last_line first_column last_column subexpressions]]]]]]

[defunk source-read [this]
  [let [[code [get this code]]]
    [let [[code_stream        [string_stream-new [string-concat code ' '] 0]]
	  [done               nil]
	  [source_expressions nil]]
      [while [not done]
	[let [[read_result [stream-try_read_source_expression code_stream]]]
	  [if [null read_result]
	      [= done `t]
	    [= source_expressions [cons read_result source_expressions]]]]]
      [reverse source_expressions]]]]

[defunk source-eval [this]
  [mapc [funk [source_expression]
	      [let [[eval_result [with-new-cause [cause-define source_expression          source_expression]
						 [eval [get source_expression body]]]]]
		[if [is-type `bug eval_result]
		    [prog [have [get bug frame] add `source_expression source_expression]
			  [bug bug]]]]]
	[source-read this]]]

[globalize file_loaded_modification_time_hash [hash]]

[defunk filename-load [this]
  [with-new-cause [cause-define source-filename this]
		  [cause-define cause-name      `filename-load]
		  [if [or [have [this-cause] lookup `load_carefully]
			  [not [have [this-cause] lookup `load_quickly]]]
		      [prog [print `[filename-load loading ,this carefully]] 
			    [source-eval [source-load this]]]
		    [prog [print `[filename-load loading ,this quickly]]
			  [primfunk:load this]]]
		  [memory-debug_assert_valid]
		  [let [[stat [pathname-stat this]]]
		    [let [[time_of_last_modification [have stat lookup `time_of_last_modification]]]
		      [have file_loaded_modification_time_hash add this time_of_last_modification]]]]
  nil]

[defunk filename-load_if_modified [this]
  [let [[file_was_loaded nil]]
    [let [[last_loaded_modification_time [have file_loaded_modification_time_hash lookup this]]]
      [if [or [null last_loaded_modification_time]
	      [let [[stat [pathname-stat this]]]
		[let [[time_of_last_modification [have stat lookup `time_of_last_modification]]]
		  [not [get last_loaded_modification_time equals time_of_last_modification]]]]]
	  [prog [package_filename-load this]
		[= file_was_loaded t]]]]
    file_was_loaded]]

[defunk package_filename-load [:rest filenames]
  [mapc [funk [filename]
	      [filename-load filename]]
	filenames]]

[defunk load-if-modified [:rest filenames]
  [let [[a_file_was_loaded nil]]
    [mapc [funk [filename]
		[let [[file_was_loaded [filename-load_if_modified filename]]]
		  [if file_was_loaded
		      [= a_file_was_loaded t]]]]
	  filenames]
    a_file_was_loaded]]

[deftypefunk symbol get is-defined [] [not [is-type `bug [shelter [eval this]]]]]

[globalize packages_frame [frame]]
[globalize packages_search_path `['built-in/alien/'
				  'built-in/ansi/'
				  'built-in/utilities/'
				  'built-in/perception_lattice/'
				  'built-in/object_lattice/'
				  'built-in/number/'
				  'built-in/math/'
				  'built-in/graphviz/'
				  'built-in/xmlrpc/'
				  'built-in/graph_match_error_correcting/'
				  'built-in/graph_cluster/'
				  'built-in/gtk/'
				  'built-in/natural_language/'
				  'built-in/simple_graph/'
				  'extension/fibermon/'
				  'extension/image/'
				  'extension/image_sequence/'
				  'extension/meta_semantic_frame_ball/'
				  'extension/meta_semantic_knowledge_base/'
				  'extension/movie/'
				  'extension/forward_planner/'
				  'extension/frame_ball/'
				  'extension/graph_isomorphism/'
				  'extension/semantic_frame_ball/'
				  'extension/semantic_knowledge_base/'
				  'example/conceptnet/'
				  'example/divisi2/'
				  'example/english_language/'
				  'example/funk2_webpage/'
				  'example/graph_match/'
				  'example/gtk-test/'
				  'example/isis_world_client/'
				  'example/isis_world_demo/'
				  'example/isismon/'
				  'example/little_carol_world/'
				  'example/macbeth/'
				  'example/mindmon/'
				  'example/moral_compass/'
				  'example/mind/'
				  'example/muddy_carol/'
				  'example/roboverse/'
				  'example/socket-client/'
				  'example/socket-server/'
				  'example/xmlrpc-test/'
				  ]]

[defmetro defpackage [name :rest dependencies]
  [let [[package_dependencies nil]
	[source_dependencies  nil]]
    [let [[iter dependencies]]
      [while iter
	[let [[key   [car  iter]]
	      [value [cadr iter]]]
	  [cond [[eq key `:packages] [= package_dependencies value]]
		[[eq key `:sources]  [= source_dependencies  value]]
		[t                   [error bug_type `unknown_package_dependency_key_type funkname `defpackage]]]]
	[= iter [cddr iter]]]]
    `[have packages_frame add [quote ,name] [new package
						 [pathname-directory_pathname source-filename]
						 [quote ,name]
						 [conslist @[mapcar [funk [package_dependency]
									  `[quote ,package_dependency]]
								    package_dependencies]]
						 [conslist @source_dependencies]]]]]

[defunk lookup-package [name]
  [have packages_frame lookup name]]

[defmetro package [name]
  `[lookup-package [quote ,name]]]

[defmetro package-defined [name]
  `[not [null [package ,name]]]]

[defunk pathnames-all_fpkg_filenames [this]
  [let [[fpkg_filenames nil]]
    [mapc [funk [pathname]
		[memory-debug_assert_valid]
		[format stdout '\nscanning ' pathname ' for package definitions.']
		[= fpkg_filenames [append fpkg_filenames [let [[result [shelter [pathname-scan_for_filenames_by_extension pathname 'fpkg']]]]
							   [if [is-type `bug result]
							       nil
							     result]]]]]
	  this]
    [memory-debug_assert_valid]
    fpkg_filenames]]

[defunk reload_all_package_definitions []
  [mapc [funk [filename]
	      [memory-debug_assert_valid]
	      [package_filename-load filename]]
	[pathnames-all_fpkg_filenames packages_search_path]]
  [memory-debug_assert_valid]]

[defunk package-load [name]
  [let [[package [lookup-package name]]]
    [mapc [funk [package_dependency]
		[package-load_if_modified package_dependency]]
	  [get package package_dependencies]]
    [with-new-cause [cause-define cause-object_types_defined nil]
		    [mapc [funk [source_dependency]
				[package_filename-load [pathname-concat [get package pathname] source_dependency]]]
			  [get package source_dependencies]]
		    [mapc [funk [object_type_defined]
				[have [get package object_types_defined_set] add object_type_defined]]
			  cause-object_types_defined]]]]

[defunk package-load_if_modified [name]
  [let [[package             [lookup-package name]]
	[loaded_a_dependency nil]
	[package_was_loaded  nil]]
    [if [null package]
	[error bug_type     `package_definition_not_loaded
	       description  [format nil 'We tried to load the package named \'' name '\', but the package does not exist.']
	       package_name name]]
    [mapc [funk [package_dependency]
		[let [[dependency_was_loaded [package-load_if_modified package_dependency]]]
		  [if dependency_was_loaded
		      [= loaded_a_dependency t]]]]
	  [get package package_dependencies]]
    [with-new-cause [cause-define cause-object_types_defined nil]
		    [mapc [funk [source_dependency]
				[let [[filename [pathname-concat [get package pathname] source_dependency]]]
				  [if loaded_a_dependency
				      [prog [package_filename-load filename]
					    [= package_was_loaded t]]
				    [let [[a_file_was_loaded [load-if-modified filename]]]
				      [if a_file_was_loaded
					  [= package_was_loaded t]]]]]]
			  [get package source_dependencies]]
		    [mapc [funk [object_type_defined]
				[have [get package object_types_defined_set] add object_type_defined]]
			  cause-object_types_defined]]
    [or loaded_a_dependency
	package_was_loaded]]]

[defmetro require [:rest names]
  `[prog [global_dlfcn_dynamic_library-unload_changed]
	 @[mapcar [funk [name]
			`[package-load_if_modified [quote ,name]]]
		  names]]]

[defmetro load-package [name]
  `[package-load [quote ,name]]]

