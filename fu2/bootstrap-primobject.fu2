'Copyright (c) 2007-2010 Bo Morgan.
 All rights reserved.
 
 Author: Bo Morgan
 
 Permission to use, copy, modify and distribute this software and its
 documentation is hereby granted, provided that both the copyright
 notice and this permission notice appear in all copies of the
 software, derivative works or modified versions, and any portions
 thereof, and that both notices appear in supporting documentation.
 
 BO MORGAN ALLOWS FREE USE OF THIS SOFTWARE IN ITS "AS IS" CONDITION.
 BO MORGAN DISCLAIMS ANY LIABILITY OF ANY KIND FOR ANY DAMAGES
 WHATSOEVER RESULTING FROM THE USE OF THIS SOFTWARE.
 
 Bo Morgan requests users of this software to return to bo@mit.edu any
 improvements or extensions that they make and grant Bo Morgan the
 rights to redistribute these changes.'

[defunk frametype-slotdef-name [this]
  [cond [[is-type `symbol this] this]
	[[is-type `cons   this] [first this]]
	[t                      [error `deframe `[compound-slotdef-name ,this]]]]]

[defunk frametype-slotdef-value [this]
  [cond [[is-type `symbol this] nil]
	[[is-type `cons   this] [second this]]
	[t                      [error `deframe `[compound-slotdef-value ,this]]]]]

[defunk type-has_parent [type_name parent_name]
  [primobject_type-has_parent_type [lookup_type type_name] parent_name]]

[defmetro with [frame_object :rest body]
  [let [[frame_object-gensym [get [stringlist-concat [conslist 'with-' [exp-as-string frame_object] '-' [gensym]]] as-symbol]]]
    `[let [[,frame_object-gensym ,frame_object]]
       [if [inherits-from frame frame_object-gensym]
	   [let [[frame_object_environment [new environment [this-env] nil]]]
	     [set frame_object_environment frame ,frame_object-gensym]
	     [let [[with_funk [funk-new_with_name_and_environment `with frame_object_environment []
								   @body]]]
	       [apply with_funk nil]]]
	 [error bug_type cannot_execute_with_object_that_does_not_inherit_from_frame
		object   ,frame_object-gensym]]]]]

[defmetro deframe [type_name parents slot_name_value_pairs :rest properties]
  [print `[deframe ,type_name ,parents ,[mapcar &frametype-slotdef-name slot_name_value_pairs]]]
  [let [[initialize_funk_args nil]
	[initialize_funk_body nil]]
    [mapc [funk [property]
		[if [not [is-type `cons property]]
		    [error bug_type `invalid_deframe_property
			   property property]
		  [let [[property_type [car property]]]
		    [cond [[eq property_type `new] [prog [= initialize_funk_args [cadr property]]
							 [= initialize_funk_body [cddr property]]]]
			  [t                       [error bug_type      `unrecognized_deframe_property_type
							  property_type property_type]]]]]]
	  properties]
    [let [[primobject_type [primobject_type-new nil]]
	  [initialize_type_funk [eval `[funk-new_with_name ,[get [stringlist-concat [conslist [exp-as-string type_name] '-initialize_type']] as-symbol] [this @initialize_funk_args]
							    [if [not [frame-contains_var this `type]]
								[frame-add_var_value this `type [quote ,type_name]]]
							    [prog @[mapcar [funk [slot_name_value_pair]
										 [let [[slot_name  [frametype-slotdef-name  slot_name_value_pair]]
										       [slot_value [frametype-slotdef-value slot_name_value_pair]]]
										   `[frame-add_var_value this [quote ,slot_name] ,slot_value]]]
									   slot_name_value_pairs]]
							    ,[let [[this-gensym               [gensym]]
								   [initialize_funk_arg_pairs [mapcar [funk [initialize_funk_arg]
													    `[,[gensym] ,initialize_funk_arg]]
												      [filter [funk [arg]
														    [not [eq arg `:rest]]]
													      initialize_funk_args]]]]
							       [let [[type_inherits_from_frame nil]]
								 [if [contains parents `frame]
								     [= type_inherits_from_frame t]
								   [mapc [funk [parent]
									       [let [[parent_primobject_type [lookup_type parent]]]
										 [if [null parent_primobject_type]
										     [error bug_type `parent_primobject_type_not_defined
											    parent_name parent]
										   [if [primobject_type-has_parent_type parent_primobject_type `frame]
										       [= type_inherits_from_frame t]]]]]
									 parents]]
								 [if type_inherits_from_frame
								     `[let [[,this-gensym this]
									    @initialize_funk_arg_pairs]
									[with this
									      [let [[this ,this-gensym]
										    @[mapcar [funk [initialize_funk_arg_pair]
												   [reverse initialize_funk_arg_pair]]
											     initialize_funk_arg_pairs]]
										@initialize_funk_body]]]
								   `[prog @initialize_funk_body]]]]]]]]
      [primobject_type-add_slot_type primobject_type `execute `initialize_type
				     initialize_type_funk]
      [primobject_type-add_slot_type primobject_type `execute `new
				     [eval `[funk-new_with_name ,[get [stringlist-concat [conslist [exp-as-string type_name] '-new']] as-symbol] ,initialize_funk_args
								 [let [[this [frame-new]]]
								   [apply ,initialize_type_funk
									   [conslist this @initialize_funk_args]]
								   this]]]]
      [primobject_type-add_slot_type primobject_type `get `type
				     [eval `[funk-new_with_name ,[get [stringlist-concat [conslist [exp-as-string type_name] '-type']] as-symbol] [this]
								 [quote ,type_name]]]]
      [primobject_type-add_slot_type primobject_type `execute `is_type
				     [eval `[funk-new_with_name ,[get [stringlist-concat [conslist [exp-as-string type_name] '-is_type']] as-symbol] [exp]
								 [and exp [frame-is_type exp] [let [[exp_frame_type_name [frame-lookup_var_value exp `type nil]]]
												[or [eq exp_frame_type_name [quote ,type_name]]
												    [type-has_parent exp_frame_type_name [quote ,type_name]]]]]]]]
      [mapc [funk [slot_name_value_pair]
		  [let [[slot_name [frametype-slotdef-name slot_name_value_pair]]]
		    [primobject_type-add_slot_type primobject_type `get slot_name
						   [eval `[funk-new_with_name ,[get [stringlist-concat [conslist [exp-as-string type_name] '-' [exp-as-string slot_name]]] as-symbol] [this]
									       [frame-lookup_var_value this [quote ,slot_name] `get-slot-does-not-exist]]]]
		    [primobject_type-add_slot_type primobject_type `set slot_name
						   [eval `[funk-new_with_name ,[get [stringlist-concat [conslist [exp-as-string type_name] '-' [exp-as-string slot_name] '-set']] as-symbol] [this value]
									       [frame-var_value-set this [quote ,slot_name] value `slot-does-not-exist]]]]]]
	    slot_name_value_pairs]
      [have primobject_type add_type_var_value `variable `parents parents]
      [add_type type_name primobject_type]]]
  nil]

[defmetro type-initialize [type_name this :rest args]
  [let [[initialize_type_funk [get [stringlist-concat [conslist [exp-as-string type_name] '-initialize_type_funk-' [exp-as-string [gensym]]]] as-symbol]]]
    `[let [[,initialize_type_funk [primobject_type-lookup_slot_type_funk [lookup_type [quote ,type_name]] `execute `initialize_type]]]
       [apply ,initialize_type_funk [conslist this @args]]]]]

[defunk add_type_funk [type_name slot_type slot_name funk]
  [let [[primobject_type [lookup_type type_name]]]
    [if [null primobject_type]
	[error bug_type    `object_type_does_not_exist
	       object_type type_name]
      [primobject_type-add_slot_type primobject_type slot_type slot_name funk]]]]

[defmetro deftypefunk [type_name slot_type slot_name args :rest body]
  [print `[deftypefunk ,type_name ,slot_type ,slot_name ,args]]
  `[add_type_funk [quote ,type_name] [quote ,slot_type] [quote ,slot_name]
		  [funk-new_with_name ,[string-as-symbol [stringlist-concat [conslist [exp-as-string type_name] '-' [exp-as-string slot_name]]]] [this @args]
				       ,[let [[this-gensym [gensym]]
					      [arg_pairs  [mapcar [funk [arg]
									`[,[gensym] ,arg]]
								  [filter [funk [arg]
										[not [eq arg `:rest]]]
									  args]]]]
					  [let [[type_inherits_from_frame nil]]
					    [if [eq type_name `frame]
						[= type_inherits_from_frame t]
					      [let [[primobject_type [lookup_type type_name]]]
						[if [null primobject_type]
						    [error bug_type `primobject_type_not_defined
							   type_name type_name]
						  [if [primobject_type-has_parent_type primobject_type `frame]
						      [= type_inherits_from_frame t]]]]]
					    [if type_inherits_from_frame
						`[let [[,this-gensym this]
						       @arg_pairs]
						   [with this
							 [let [[this ,this-gensym]
							       @[mapcar [funk [arg_pair]
									      [reverse arg_pair]]
									arg_pairs]]
							   @body]]]
					      `[prog @body]]]]]]]

