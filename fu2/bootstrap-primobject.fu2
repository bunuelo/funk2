'Copyright (c) 2007-2012 Bo Morgan.
 All rights reserved.
 
 Author: Bo Morgan
 
 Permission to use, copy, modify and distribute this software and its
 documentation is hereby granted, provided that both the copyright
 notice and this permission notice appear in all copies of the
 software, derivative works or modified versions, and any portions
 thereof, and that both notices appear in supporting documentation.
 
 BO MORGAN ALLOWS FREE USE OF THIS SOFTWARE IN ITS "AS IS" CONDITION.
 BO MORGAN DISCLAIMS ANY LIABILITY OF ANY KIND FOR ANY DAMAGES
 WHATSOEVER RESULTING FROM THE USE OF THIS SOFTWARE.
 
 Bo Morgan requests users of this software to return to bo@mit.edu any
 improvements or extensions that they make and grant Bo Morgan the
 rights to redistribute these changes.'


`[defunk primobject-frametype-slotdef-name-old [this]
  [cond [[is-type `symbol this] this]
	[[is-type `cons   this] [first this]]
	[t                      [error `deframe `[compound-slotdef-name ,this]]]]]

`[defunk primobject-frametype-slotdef-value-old [this]
  [cond [[is-type `symbol this] nil]
	[[is-type `cons   this] [second this]]
	[t                      [error `deframe `[compound-slotdef-value ,this]]]]]

`[defunk primobject-frametype-type-has_parent-old [type_name parent_name]
  [primobject_type-has_parent_type [lookup_type type_name] parent_name]]

`[defmetro with-old [frame_object :rest body]
  [let [[frame_object-gensym [gensym 'with']]]
    `[let [[,frame_object-gensym ,frame_object]]
       [if [object-inherits_from ,frame_object-gensym `frame]
	   [let [[frame_object_environment [new environment ,frame_object-gensym [this-env] nil]]]
	     [let [[with_funk [funk-new_with_name_and_environment with frame_object_environment []
								  @body]]]
	       [apply with_funk nil]]]
	 [error bug_type `cannot_execute_with_object_that_does_not_inherit_from_frame
		object   ,frame_object-gensym]]]]]

[defmetro create [type_name]
  [let [[type_create_funk [get [stringlist-concat [conslist [exp-as-string type_name] '-' [exp-as-string [gensym 'type_create_funk']]]] as-symbol]]]
    `[let [[,type_create_funk [primobject_type-lookup_slot_type_funk [lookup_type [quote ,type_name]] `execute `type_create]]]
       [if ,type_create_funk
	   [apply ,type_create_funk [cons this nil]]]]]]

[defmetro construct [type_name :rest args]
  [let [[type_construct_funk [get [stringlist-concat [conslist [exp-as-string type_name] '-' [exp-as-string [gensym 'type_construct_funk']]]] as-symbol]]]
    `[let [[,type_construct_funk [primobject_type-lookup_slot_type_funk [lookup_type [quote ,type_name]] `execute `type_construct]]]
       [if ,type_construct_funk
	   [apply ,type_construct_funk [conslist this @args]]]]]]

[defmetro deframe [type_name parents slot_name_value_pairs :rest properties]
  [let [[type_inherits_from_frame nil]]
    [if [contains parents `frame]
	[= type_inherits_from_frame t]
      [mapc [funk [parent]
		  [let [[parent_primobject_type [lookup_type parent]]]
		    [if [null parent_primobject_type]
			[error bug_type    `parent_primobject_type_not_defined
			       parent_name parent]
		      [if [primobject_type-has_parent_type parent_primobject_type `frame]
			  [= type_inherits_from_frame t]]]]]
	    parents]]
    [if [not type_inherits_from_frame]
	[= parents [cons `frame parents]]]]
  [terminal_format standard-terminal '\n' `[deframe ,type_name ,parents]]
  [let [[construct_funk_args nil]
	[construct_funk_body nil]]
    [mapc [funk [property]
		[if [not [is-type `cons property]]
		    [error bug_type `invalid_deframe_property
			   property property]
		  [let [[property_type [car property]]]
		    [cond [[eq property_type `new] [prog [= construct_funk_args [cadr property]]
							 [= construct_funk_body [cddr property]]]]
			  [t                       [error bug_type      `unrecognized_deframe_property_type
							  property_type property_type]]]]]]
	  properties]
    [let [[primobject_type [primobject_type-new nil]]
	  [type_create_funk [eval `[funk-new_with_name ,[get [stringlist-concat [conslist [exp-as-string type_name] '-type_create']] as-symbol] [this]
							[if [not [frame-contains_var this `type]]
							    [frame-add_var_value this `type [quote ,type_name]]]
							[prog @[mapcar [funk [parent]
									     `[create ,parent]]
								       parents]]
							@[mapcar [funk [slot_name_value_pair]
								       [let [[slot_name  [primobject-frametype-slotdef-name  slot_name_value_pair]]
									     [slot_value [primobject-frametype-slotdef-value slot_name_value_pair]]]
									 `[frame-add_var_value this [quote ,slot_name] ,slot_value]]]
								 slot_name_value_pairs]
							 ]]]
	  [type_construct_funk [eval `[funk-new_with_name ,[get [stringlist-concat [conslist [exp-as-string type_name] '-type_construct']] as-symbol] [this @construct_funk_args]
							  ,[let [[this-gensym              [gensym 'this']]
								 [construct_funk_arg_pairs [mapcar [funk [construct_funk_arg]
													 `[,[gensym 'arg'] ,construct_funk_arg]]
												   [filter [funk [arg]
														 [not [eq arg `:rest]]]
													   construct_funk_args]]]]
							     `[let [[,this-gensym this]
								    @construct_funk_arg_pairs]
								[with this
								      [let [[this ,this-gensym]
									    @[mapcar [funk [construct_funk_arg_pair]
											   [reverse construct_funk_arg_pair]]
										     construct_funk_arg_pairs]]
									@construct_funk_body]]]
							     ]]]]]
      [primobject_type-add_slot_type primobject_type `execute `type_create
				     type_create_funk]
      [primobject_type-add_slot_type primobject_type `execute `type_construct
				     type_construct_funk]
      [primobject_type-add_slot_type primobject_type `execute `new
				     [eval `[funk-new_with_name ,[get [stringlist-concat [conslist [exp-as-string type_name] '-new']] as-symbol] ,construct_funk_args
								 [let [[this [frame-new]]]
								   [apply ,type_create_funk
									   [cons this nil]]
								   [apply ,type_construct_funk
									   [conslist this @construct_funk_args]]
								   this]]]]
      [primobject_type-add_slot_type primobject_type `get `type
				     [eval `[funk-new_with_name ,[get [stringlist-concat [conslist [exp-as-string type_name] '-type']] as-symbol] [this]
								 [quote ,type_name]]]]
      [primobject_type-add_slot_type primobject_type `execute `is_type
				     [eval `[funk-new_with_name ,[get [stringlist-concat [conslist [exp-as-string type_name] '-is_type']] as-symbol] [exp]
								 [and exp [frame-is_type exp] [let [[exp_frame_type_name [frame-lookup_var_value exp `type nil]]]
												[or [eq exp_frame_type_name [quote ,type_name]]
												    [primobject-frametype-type-has_parent exp_frame_type_name [quote ,type_name]]]]]]]]
      [mapc [funk [slot_name_value_pair]
		  [let [[slot_name [primobject-frametype-slotdef-name slot_name_value_pair]]]
		    [if [primobject_type-lookup_slot_type_funk primobject_type `get slot_name]
			[error bug_type  `deframe-redefining-slot
			       type_name type_name
			       slot_name slot_name]]
		    [primobject_type-add_slot_type primobject_type `get slot_name
						   [eval `[funk-new_with_name ,[get [stringlist-concat [conslist [exp-as-string type_name] '-' [exp-as-string slot_name]]] as-symbol] [this]
									      [frame-lookup_var_value this [quote ,slot_name] `get-slot-does-not-exist]]]]
		    [primobject_type-add_slot_type primobject_type `set slot_name
						   [eval `[funk-new_with_name ,[get [stringlist-concat [conslist [exp-as-string type_name] '-' [exp-as-string slot_name] '-set']] as-symbol] [this value]
									      [frame-var_value-set this [quote ,slot_name] value `slot-does-not-exist]]]]]]
	    slot_name_value_pairs]
      [have primobject_type add_type_var_value `variable `parents parents]
      [add_type type_name primobject_type]]]
  nil]

`[defunk primobject-frametype-add_type_funk-old [type_name slot_type slot_name funk]
  [let [[primobject_type [lookup_type type_name]]]
    [if [null primobject_type]
	[error bug_type    `object_type_does_not_exist
	       object_type type_name]
      [primobject_type-add_slot_type primobject_type slot_type slot_name funk]]]]

[defunk remove_type_funk [type_name slot_type slot_name]
  [let [[primobject_type [lookup_type type_name]]]
    [if [null primobject_type]
	[error bug_type    `object_type_does_not_exist
	       object_type type_name]
      [primobject_type-remove_slot_type primobject_type slot_type slot_name]]]]

[defmetro deftypefunk [type_name slot_type slot_name args :rest body]
  [if [and [not [eq slot_type `get]]
	   [not [eq slot_type `set]]
	   [not [eq slot_type `execute]]]
      [error bug_type         `deftypefunk_invalid_slot_type
	     valid_slot_types `[get set execute]
	     slot_type        slot_type]]
  [terminal_format standard-terminal '\n' `[deftypefunk ,type_name ,slot_type ,slot_name ,args]]
  `[prog [if [object_type-type-funk [quote ,type_name] [quote ,slot_type] [quote ,slot_name]]
	     [terminal_format standard-terminal '\ndeftypefunk-warning: redefining ' [quote ,type_name] ' ' [quote ,slot_type] ' ' [quote ,slot_name] '.']]
	 [primobject-frametype-add_type_funk [quote ,type_name] [quote ,slot_type] [quote ,slot_name]
					     [funk-new_with_name ,[string-as-symbol [stringlist-concat [conslist [exp-as-string type_name] '-' [exp-as-string slot_type] '-' [exp-as-string slot_name]]]] [this @args]
								 ,[let [[this-gensym [gensym 'this']]
									[arg_pairs  [mapcar [funk [arg]
												  `[,[gensym 'arg'] ,arg]]
											    [filter [funk [arg]
													  [not [eq arg `:rest]]]
												    args]]]]
								    [let [[type_inherits_from_frame nil]]
								      [if [eq type_name `frame]
									  [= type_inherits_from_frame t]
									[let [[primobject_type [lookup_type type_name]]]
									  [if [null primobject_type]
									      [error bug_type `primobject_type_not_defined
										     type_name type_name]
									    [if [primobject_type-has_parent_type primobject_type `frame]
										[= type_inherits_from_frame t]]]]]
								      [if type_inherits_from_frame
									  `[let [[,this-gensym this]
										 @arg_pairs]
									     [with this
										   [let [[this ,this-gensym]
											 @[mapcar [funk [arg_pair]
													[reverse arg_pair]]
												  arg_pairs]]
										     @body]]]
									`[prog @body]]]]]]]]

[defmetro undeftypefunk [type_name slot_type slot_name]
  [if [and [not [eq slot_type `get]]
	   [not [eq slot_type `set]]
	   [not [eq slot_type `execute]]]
      [error bug_type         `undeftypefunk_invalid_slot_type
	     valid_slot_types `[get set execute]
	     slot_type        slot_type]]
  [terminal_format standard-terminal '\n' `[undeftypefunk ,type_name ,slot_type ,slot_name]]
  `[if [not [object_type-type-funk [quote ,type_name] [quote ,slot_type] [quote ,slot_name]]]
       [terminal_format standard-terminal '\nundeftypefunk-warning: not defined, so not undefining ' [quote ,type_name] ' ' [quote ,slot_type] ' ' [quote ,slot_name] '.']
     [remove_type_funk [quote ,type_name] [quote ,slot_type] [quote ,slot_name]]]]

