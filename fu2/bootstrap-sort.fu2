'Copyright (c) 2007-2008 Bo Morgan.
 All rights reserved.
 
 Author: Bo Morgan
 
 Permission to use, copy, modify and distribute this software and its
 documentation is hereby granted, provided that both the copyright
 notice and this permission notice appear in all copies of the
 software, derivative works or modified versions, and any portions
 thereof, and that both notices appear in supporting documentation.
 
 BO MORGAN ALLOWS FREE USE OF THIS SOFTWARE IN ITS "AS IS" CONDITION.
 BO MORGAN DISCLAIMS ANY LIABILITY OF ANY KIND FOR ANY DAMAGES
 WHATSOEVER RESULTING FROM THE USE OF THIS SOFTWARE.
 
 Bo Morgan requests users of this software to return to bo@mit.edu any
 improvements or extensions that they make and grant Bo Morgan the
 rights to redistribute these changes.'


[defunk quicksort_array_helper [a first_element last_element compare]
  [let [[key nil]
	[i   nil]
	[j   nil]
	[k   nil]]
    [if [< first_element last_element]
	[prog [= k [/ [+ first_element last_element] 2]]
	      [let [[temp [get a elt first_element]]]
		[set           a elt k             [get a elt first_element]]
		[set           a elt first_element temp]]
	      [= key [get a elt first_element]]
	      [= i [+ first_element 1]]
	      [= j last_element]
	      [while [<= i j]
		[while [and [<= i last_element] [not [funkall compare [get a elt i] key]]]
		  [= i [+ i 1]]]
		[while [and [>= j first_element] [funkall compare [get a elt j] key]]
		  [= j [- j 1]]]
		[if [< i j]
		    [let [[temp [get a elt i]]]
		      [set a elt i [get a elt j]]
		      [set a elt j temp]]]]
	      [let [[temp [get a elt first_element]]]
		[set a elt first_element [get a elt j]]
		[set a elt j             temp]]
	      [quicksort_array_helper a first_element [- j 1] compare]
	      [quicksort_array_helper a [+ j 1] last_element compare]
	      ]]
    ]]

[defunk quicksort_array [a strict_comparison_funk]
  [quicksort_array_helper a 0 [- [length a] 1] strict_comparison_funk]
  a]
[set &quicksort_array documentation 'sorts an array in place according to the user-provided strict comparison funkable (i.e. <, >, and not <= or >=)']

[globalize-funk sort_array &quicksort_array]


